plugins {
	id 'java'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'net.kyori.blossom' version '2.1.0'
	id 'xyz.jpenilla.run-velocity' version '2.2.3'
}

base {
	archivesName = project.archives_base_name
}

version = project.plugin_version
group = project.maven_group

configurations {
	shade
}

repositories {
	maven { url = "https://papermc.io/repo/repository/maven-public/" }
	maven { url = "https://repo.sleeping.town/" }
	mavenCentral()
}

dependencies {
	annotationProcessor(compileOnly("com.velocitypowered:velocity-api:${project.velocity_version}"))

	implementation shade("net.dv8tion:JDA:${project.jda_version}") {
		exclude module: 'opus-java'
	}
	implementation shade("folk.sisby:kaleido-config:${project.kaleido_version}")
}

compileJava {
	options.encoding = "UTF-8"
	options.release = 17
}

java {
	withSourcesJar()
}

build {
	dependsOn shadowJar
}

shadowJar {
	setArchiveClassifier(null)
	setConfigurations([project.configurations.shade])
	relocate("net.dv8tion", "cc.unilock.discordlib.lib.net.dv8tion")
	relocate("folk.sisby", "cc.unilock.discordlib.lib.folk.sisby")
	//minimize()
}

runVelocity {
	velocityVersion(project.velocity_version)
}

sourceSets {
	main {
		blossom {
			javaSources {
				property "version", project.version
			}
		}
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
